;;; site-lisp/user-utils.el

;;; ÈõëÂ§ö„Å™Èñ¢Êï∞Áæ§ or Ëá™‰ΩúÈñ¢Êï∞„Å´‰æùÂ≠ò„Åô„Çã„ÇÇ„ÅÆ

(require 'subr-x)
(require 'cl-lib)

(declare-function json-read "json")

(defalias 'standard-error-output #'external-debugging-output
  "Standard error output (stderr).
Example:
  (print \"HELLO\" #\\='standard-error-output)
  (let ((standard-output #\\='standard-error-output))
    (message \"HELLO\"))")

(defun println (obj &optional out)
  (princ obj out)
  (terpri out))

(defun sequence (from to &optional step)
  (if (< from to)
      (number-sequence from to step)
      (nreverse (number-sequence to from step))))

(defun indent-line-sexp ()
  "Èï∑„ÅÑ‰∏ÄË°åSÂºè„Çí„Åù„Çå„Å™„Çä„Å´„Ç§„É≥„Éá„É≥„Éà„Åó„Åæ„Åô."
  (interactive)
  (save-excursion
    (save-restriction
      ;; (while (condition-case nil (backward-up-list) (error nil)))
      (narrow-to-region (point)
                        (save-excursion
                          (forward-sexp)
                          (point)))
      (perform-replace ") " ")\n" nil  nil nil)))
  (indent-sexp))

(fset 'trim-tailing-whitespace #'delete-trailing-whitespace)

(defun insert-zenkaku-space ()
  "ÂÖ®ËßíÁ©∫ÁôΩ„ÇíÊåøÂÖ•."
  (interactive)
  (insert (japanese-zenkaku " ")))

(fset 'insert-full-width-space #'insert-zenkaku-space)
(fset 'hankaku-region #'japanese-hankaku-region)
(fset 'zenkaku-region #'japanese-zenkaku-region)

(defun sha1-file (filename)
  "„Éï„Ç°„Ç§„É´„ÅÆ„ÉÅ„Çß„ÉÉ„ÇØ„Çµ„É†„ÇíÂá∫Âäõ„Åó„Åæ„Åô."
  (require 'sha1)
  (sha1 (with-temp-buffer
          (insert-file-contents-literally filename)
          (buffer-string))))

(defalias 'sha1sum #'sha1-file)

(defun describe-bindings-anymap (keymap)
  "„ÅÇ„Çâ„ÇÜ„Çã KEYMAP „ÅÆ„Ç≠„Éº„Éû„ÉÉ„Éó„ÇíË°®Á§∫„Åó„Åæ„Åô."
  (interactive (list (intern (completing-read "Keymap: " obarray (lambda (s)
                                                                   (and (boundp s)
                                                                        (keymapp s)))))))
  (cl-check-type (symbol-value keymap) keymap)
  (with-help-window (help-buffer)
    (with-current-buffer (help-buffer)
      (insert (substitute-command-keys (format "\\{%s}" keymap))))))

(defun circular-list-p (object)
  "Returns true if OBJECT is a circular list, NIL otherwise."
  (and (listp object)
       (eq (cl-list-length object) nil)))

(defun arglist (def)
  "Get DEF arguments.

(arglist 'lambda)  => \"(lambda ARGS [DOCSTRING] [INTERACTIVE] BODY)\"
(arglist 'arglist) => (def)
"
  (when (fboundp def)
    (or (car (help-split-fundoc (documentation def) def))
        (help-function-arglist def))))

(defun iso8601 (&optional time universal)
  "Return ISO 8601 format time string."
  ;; (format-time-string "%Y-%m-%dT%H:%M:%S")
  (format-time-string "%FT%T%z" time universal))

(defun unix-time-at-point ()
  "„Éù„Ç§„É≥„Éà‰ΩçÁΩÆ„ÅÆÊï∞ÂÄ§„Çí UNIX ÊôÇÈñì„Å®„Åø„Å™„Åó„ÅüÂ†¥Âêà„ÅÆÊôÇÂàª„ÇíË°®Á§∫„Åó„Åæ„Åô."
  (interactive)
  (let ((n (thing-at-point 'number)))
    (message "%d => %s" n (iso8601 n))))

(defun thing-at-point-to (thing fn)
  "„Éù„Ç§„É≥„Éà‰ΩçÁΩÆ„ÅÆÊÉÖÂ†±„ÇíÂü∫„Å´ (funcall FN (thing-at-point THING)) „ÅÆÁµêÊûú„ÇíË°®Á§∫„Åô„Çã."
  (interactive
   (let ((things '(symbol list sexp defun filename url email word sentence whitespace line number page)))
     (list (intern (completing-read "Thing at point as: " things nil t))
           (intern (completing-read "To command: " obarray 'fboundp t)))))
  (let* ((val (thing-at-point thing))
         (ans (funcall fn val)))
    (message "(funcall #'%S %S) ;=> %S" fn val ans)))

(defun point-of (fn)
  "FN ÂÆüË°åÂæå„ÅÆ„Éù„Ç§„É≥„Éà‰ΩçÁΩÆ„ÇíËøî„Åô.

(point-of #\\='beginning-of-buffer) => 1"
  (save-window-excursion
    (save-excursion
      (funcall fn)
      (point))))

(defun delete-backward-word (&optional n)
  "Áõ¥Ââç„ÅÆÂçòË™û„ÇíÂâäÈô§„Åô„Çã."
  (interactive "p")
  (unless (integerp n)
    (signal 'wrong-type-argument `(integerp ,n)))
  (delete-region (point)
                 (progn (backward-word n) (point))))


(defun plist->alist (plist)
  "Translate PLIST to alist.
property list (:key value ...)
association list ((key . value) ...)"
  (cl-labels ((symbol (sym)
                (if (keywordp sym) (intern (substring (symbol-name sym) 1)) sym)))
    (cl-loop for (key value) on plist by #'cddr
           collect (cons (symbol key) value))))

(defun alist->plist (alist)
  "Translate ALIST to plist.
property list is (:key value ...)
association list is ((key . value) ...)"
  (cl-labels ((keyword (sym)
                (if (keywordp sym) sym (intern (format ":%s" sym)))))
    (let (plist)
      (cl-loop for (key . value) in alist
               do (setq plist (plist-put plist (keyword key) value)))
      plist)))

;; http://ja.stackoverflow.com/a/24433/2391
(defun get-in (object keys)
  "Returns the value in a nested assoc or vector OBJECT used by KEYS.

Example:
(get-in object [key1 key2 0]) => value"
  (cl-reduce #'(lambda (obj key)
                 (cl-typecase key
                   (integer (elt obj key))
                   ((or symbol string) (cdr (assoc key obj)))
                   (otherwise (user-error "Unknown key type: %S" key))))
             keys
             :initial-value object))

(defun buffer-needs-save-p (buffer)
  "[user] Return non-nil if the visited-file BUFFER is still modified."
  (and (buffer-file-name buffer)
       (buffer-modified-p buffer)))

;;(seq-filter #'buffer-needs-save-p (buffer-list))

(defun occur-all-buffers (regexp)
  "[user] Search REGEXP in all buffers."
  (interactive "sSearch buffers: ")
  (multi-occur (buffer-list) regexp)
  ;;(multi-occur-in-matching-buffers "." regexp t)
  )

(defun module-feature-p ()
  (fboundp 'module-load))

(defun Math.random ()
  "Return [0.0, 1.0) real number."
  (/ (random) (+ 1.0 most-positive-fixnum)))

(defun rand (min max)
  "Return [MIN, MAX] random number."
  (+ min (% (random) (+ max 1 (- min))))
  ;;(+ min (floor (* (random) (+ max (- min) 1.0)) (+ 1.0 most-positive-fixnum)))
  )

(defun rand* (min max)
  "Return [MIN, MAX) random number."
  (error "Not implement yet"))

(defun zalgo (text &optional depth)
  "Zalgo Text Generator (incomplete)."
  (replace-regexp-in-string
   (rx alnum)
   (lambda (s)
     (concat s (with-output-to-string
                 (dotimes (_ (or depth 30))
                   (princ (string (rand #x0300 #x036F)))))))
   text t t))

(defun zalgo-buffer (&optional depth)
  (interactive "*")
  (save-excursion
    (goto-char (point-min))
    (while (re-search-forward "[A-Za-z0-9]" nil t)
      (replace-match (concat (match-string 0)
                             (with-output-to-string
                               (dotimes (_ (or depth 5))
                                 (princ (string (rand #x0300 #x036F))))))
                     nil t))))

(defun emacs-help-options ()
  "[user] Display \"emacs --help\" text."
  (interactive)
  (let ((emacs (expand-file-name invocation-name invocation-directory)))
    (with-help-window (help-buffer)
      (call-process emacs nil standard-output t "--help"))))

(defun emacsclient-help-options ()
  "[user] Display \"emacsclient --help\" text."
  (interactive)
  (let ((emacs (expand-file-name "emacsclient" invocation-directory)))
    (with-help-window (help-buffer)
      (call-process emacs nil standard-output t "--help"))))

(defun user::exec* (program infile &rest args)
  (with-output-to-string
    (apply #'call-process program infile standard-output t args)))

(defun user::exec (program input &rest args)
  "Execute PROGRAM+ARGS, then return the result as string.
INPUT (filename/buffer/nil) is used as a process standard input."
  (if (bufferp input)
      (let ((infile (make-temp-file "xyz")))
        (unwind-protect
            (progn
              (with-temp-file infile
                (insert-buffer-substring input))
              (apply #'user::exec* program infile args))
          (and (file-exists-p infile)
               (delete-file infile))))
    (apply #'user::exec* program input args)))

;; TODO: suppress other echo-area output (eldoc, etc)
(define-minor-mode what-cursor-position-minor-mode
  "Turn on/off `what-cursor-position'."
  nil " üîç" nil
  (if what-cursor-position-minor-mode
      (add-hook 'post-command-hook #'what-cursor-position t 'local)
    (remove-hook 'post-command-hook #'what-cursor-position 'local)))

(defun xlfd-at (pos)
  (if (= pos (point-max))
      "[EOF]"
    (if-let ((font (font-at pos)))
        (font-xlfd-name font)
      "[Tofu]")))

(defun user::process-environment-alist ()
  (mapcar (lambda (e)
            (if-let ((pos (seq-position e ?=)))
                (cons (substring e 0 pos) (substring e (1+ pos)))
              (cons e nil)))
          process-environment))

(defun user::jump-to-env-directory (env-name)
  "Áí∞Â¢ÉÂ§âÊï∞ `ENV-NAME' „ÅåÁ§∫„Åô„Éá„Ç£„É¨„ÇØ„Éà„É™„Å´„Ç∏„É£„É≥„Éó„Åó„Åæ„Åô."
  (interactive
   (list (completing-read "Jump to Env Directory: "
           (cl-remove-if-not
            (pcase-lambda (`(,_name . ,val))
              (and (stringp val)
                   (file-directory-p val)))
            (user::process-environment-alist)))))
  (dired (getenv env-name)))

(defun iterate-text-prop (prop func)
  "Iterate each buffer property PROP, and run with function FUNC.
URL `http://emacs.g.hatena.ne.jp/kiwanami/20110809/1312877192'"
  (cl-loop with pos = (point-min)
           for next = (next-single-property-change pos prop)
           for text-val = (and next (get-text-property next prop))
           while next do
           (when text-val
             (funcall func pos next text-val))
           (setq pos next)))

;; e.g. face „ÅÆ‰ªò„ÅÑ„Å¶„ÅÑ„ÇãÈ†òÂüü„Å´„Å§„ÅÑ„Å¶„É´„Éº„Éó„Åô„Çã
'
(iterate-text-prop 'face 
  (lambda (begin end val)
    (let ((print-escape-newlines t))
      (and val (message ">> %S : %S" val (buffer-substring-no-properties begin end))))))

(defun shredder (string)
  "ÊñáÂ≠óÂàó STRING „Çí‰ºè„ÅõÂ≠ó„Å´Â§âÊèõ„Åó„Åæ„Åô."
  (replace-regexp-in-string "[[:alnum:]]" "*" string))

(defun shredder-region (start end)
  "ÈÅ∏Êäû„Åó„ÅüÁØÑÂõ≤„Çí******„Åó„Åæ„Åô."
  (interactive "*r")
  (save-excursion
    (save-restriction
      (narrow-to-region start end)
      (goto-char (point-min))
      (while (re-search-forward "[[:alnum:]]" nil t)
        (replace-match "*")))))

(defun outlineapi-fetch (article-url)
  (let ((url-request-extra-headers
         '(("Referer" . "https://outline.com/")))
        (url (concat "https://outlineapi.com/article?"
                     (url-build-query-string
                      `((source_url ,article-url))))))
    (with-temp-buffer
      (url-insert-file-contents url)
      (json-read))))

(defun user::url-parse (url)
  "„ÇØ„Ç®„É™ÊñáÂ≠óÂàó„ÇíÂê´„ÇÅ„ÅüURL„ÅÆËß£Êûê„ÇíË°å„ÅÑ„Åæ„Åô.
Return-Type => ((urlobj struct-url) . (query alist))"
  (pcase-let* ((urlobj (url-generic-parse-url url))
               (`(,path . ,query-string) (url-path-and-query urlobj))
               (query (if query-string (url-parse-query-string query-string))))
    (setf (url-filename urlobj) path)
    (cons urlobj query)))

(defun user::unique-color-name (obj)
  "Return a unique-like supported color name from OBJ."
  (let* ((colors (defined-colors))
         (n (mod (sxhash obj) (length colors))))
    (nth n colors)))

(defun user::unique-color-hex (obj)
  "Return a unique-like hexadecimal color identifier from OBJ."
  (let* ((ncolors (display-color-cells))
         (rgb (mod (sxhash obj) (1- ncolors)))
         (fmt (cond
                ((= ncolors 16777216) "#%06x") ; 24-bit #RRGGBB
                ((= ncolors      256) "#%03x") ; 8-bit  #RGB
                )))
    (format fmt rgb)))

(defun gc-with-message ()
  "Show memory usage.
See URL `https://www.reddit.com/r/emacs/comments/ck4zb3/'
Or `chart-emacs-storage'"
  (interactive)
  (display-message-or-buffer
   (cl-loop for (name size used free) in (garbage-collect)
            for used = (* used size)
            for free = (* (or free 0) size)
            for total = (file-size-human-readable (+ used free))
            for used = (file-size-human-readable used)
            for free = (file-size-human-readable free)
            concat (format "%s: %s + %s = %s\n" name used free total))))

(provide 'user-utils)

;;; user-utils.el ends here
